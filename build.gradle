import java.util.regex.Pattern

buildscript {
    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter {
            url "http://jcenter.bintray.com/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.1'
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    version = '1.0'
    ext {
        appName = '3DReno'
        gdxVersion = '1.9.4'
        roboVMVersion = '2.2.0'
        box2DLightsVersion = '1.3'
        ashleyVersion = '1.3.1'
        aiVersion = '1.5.0'
    }

    repositories {
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
}

project(":desktop") {
    apply plugin: "java"


    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"

        compile "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"

        compile ("com.thoughtworks.xstream:xstream:1.4.2"){
        }
    }
}

project(":android") {
    apply plugin: "android"

    configurations { natives }

    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"

        compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86"

        compile 'com.google.android.gms:play-services-analytics:8.4.0'
    }

    task updateManifest << {
        new ByteArrayOutputStream().withStream { os ->

            // get git version
            def result = exec {
                executable = 'sh'
                args = ["-c", "git log --pretty=format:'' | wc -l"]
                standardOutput = os
            }

            // open base file
            def manifestBaseFile = file("android/manifest/AndroidManifest.xml")
            def manifestFile = file("android/AndroidManifest.xml")
            def manifestContent = manifestBaseFile.getText()

            // pattern for revision
            def rev = os.toString().trim()
            def pattern_rev = Pattern.compile("(__revision__)")
            def matcher_rev = pattern_rev.matcher(manifestContent)
            matcher_rev.find()
            println "Setting __revision__ to ${rev} in AndroidManifest.xml"
            manifestContent = matcher_rev.replaceAll(rev)

            // pattern for package
            def pkg = gradle.pro ? "fr.limsi.rorqual" : "fr.limsi.rorqual.free"
            def pattern_pkg = Pattern.compile("(__package__)")
            def matcher_pkg = pattern_pkg.matcher(manifestContent)
            matcher_pkg.find()
            println "Setting __package__ to ${pkg} in AndroidManifest.xml"
            manifestContent = matcher_pkg.replaceAll(pkg)

            // pattern for icon
            def icon = gradle.pro ? "ic_launcher" : "ic_launcher_free"
            def pattern_icn = Pattern.compile("(__icon__)")
            def matcher_icn = pattern_icn.matcher(manifestContent)
            matcher_icn.find()
            println "Setting __icon__ to ${icon} in AndroidManifest.xml"
            manifestContent = matcher_icn.replaceAll(icon)

            manifestFile.write(manifestContent)
        }
    }

    preBuild.dependsOn updateManifest

}

project(":jcsg") {
}

project(":core") {
    apply plugin: "java"

    dependencies {
        compile project(":jcsg")

        if (gradle.use_javatoolbox) compile project(":ifc2x3javatoolbox") else compile project(":ifc2x3native")

        compile "com.badlogicgames.gdx:gdx:$gdxVersion"

        compile "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"

        compile ("com.thoughtworks.xstream:xstream:1.4.2"){
            exclude group: 'xmlpull'
        }

    }
}

tasks.eclipse.doLast {
    delete ".project"
}
